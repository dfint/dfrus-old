-- Утилита для удаления текста, находящегося вне тега <pre></pre>
include std/search.e
include std/text.e

constant lookup_table = {
    {"quot","lt","gt","amp","#160"},
    {"\"","<",">","&"," "}
}

function convert_special_symbols(sequence s)
    integer from=1, i, j
    while 1 do
        i = find('&',s,from)
        if i=0 then
            exit
        end if
        j = find(';',s,i+1)
        if j = 0 then
            continue
        end if
        object x = lookup(s[i+1..j-1], lookup_table[1], lookup_table[2])
        if sequence(x) then
            s = replace(s, x, i, j)
        end if
        from = i+1
    end while
    return s
end function

integer on = 0
integer r = 0
while 1 do
    object line = gets(0)
    if atom(line) then
        exit
    end if
    
    line = convert_special_symbols(line)
    
    if not on then
        integer pre = match("<pre>",line)
        if pre then
            on = 1
            line = line[pre+5..$]
            if length(line)=1 then -- игнорировать строку, содержащую только <pre>
                continue
            end if
        else
            continue
        end if
    elsif ends("</pre>\n",line) then
        on = 0
        line = line[1..$-7]
        if length(line)=0 then -- игнорировать строку, содержащую только </pre>
            continue
        end if
    end if
    
    if r then
        puts(1,'\n')
    end if
    
    if line[$] = '\n' then
        r = 1
        line = line[1..$-1]
    else
        r = 0
    end if
    
    puts(1,line)
end while
